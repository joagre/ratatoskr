#!/usr/bin/env escript
%%! -smp enable -sname peg_debugger

main([]) ->
    Grammar = read_file_on_stdin(),
    ModifiedGrammar = add_actions(re:split(Grammar, "\n", [{return, list}])),
    io:format("~s\n", [ModifiedGrammar]).

read_file_on_stdin() ->
    lists:flatten(read_lines()).

read_lines() ->
    case io:get_line('') of
        eof ->
            [];
        Line ->
            [Line|read_lines()]
    end.

add_actions([]) ->
    "";
add_actions([Line|Rest]) ->
    case re:run(Line, "(\\w+)\\s*<-\\s*(.*)", [{capture, all_but_first, list}]) of
        {match, [RuleName, _]} ->
            {RemainingLines, MoreLines} = until_next_rule(Rest),
            DebugAction =
                io_lib:format(" { fprintf(stderr, \"~s\\n\"); }\n", [RuleName]),
            Line ++ MoreLines ++ DebugAction ++ add_actions(RemainingLines);
        nomatch ->
            Line ++ "\n" ++ add_actions(Rest)
    end.

until_next_rule(Lines) ->
    until_next_rule(Lines, "").

until_next_rule(["#" ++ _|Rest], MoreLines) ->
    until_next_rule(Rest, MoreLines);
until_next_rule([""|Rest], MoreLines) ->
    until_next_rule(Rest, MoreLines);
until_next_rule([], MoreLines) ->
    {[], string:chomp(MoreLines)};
until_next_rule(["%" ++ _ = Line|Rest], MoreLines) ->
    {[Line|Rest], MoreLines};
until_next_rule([Line|Rest], MoreLines) ->
    case re:run(Line, "(\\w+)\\s*<-\\s*(.*)", [{capture, all_but_first, list}]) of
        {match, _} ->
            {[Line|Rest], MoreLines};
        _ ->
            until_next_rule(Rest, MoreLines ++ "\n" ++ Line)
    end.
