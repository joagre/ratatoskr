%prefix "satie"

%header {
    #include <stdarg.h>
    #include <satie_error.h>
    #include "satie_auxil.h"
    #include "symbol_table.h"
    #include "hm.h"

/*
    static const char *dbg_str[] = { "Evaluating rule", "Matched rule", "Abandoning rule" };
    #define PCC_DEBUG(auxil, event, rule, level, pos, buffer, length) \
    if (strcmp(rule, "WS") != 0 && strcmp(rule, "_") != 0 && strcmp(rule, "__") != 0) \
        fprintf(stderr, "%*s%s %s @%zu [%.*s]\n", (int)((level) * 2), "", dbg_str[event], \
                rule, pos, (int)(length), buffer)
*/

    static int ROW = 1;

    static int satie_getchar(satie_auxil_t* _auxil) {
        int c = getchar();
        if (c == '\n') {
            ROW++;
        }
        return c;
    }

    static void panic(const char *fmt, ...) {
        va_list args;
        va_start(args, fmt);
        printf("\033[31mError:\033[0m ");
        fflush(stdout);
        vfprintf(stderr, fmt, args);
        printf("\n");
        va_end(args);
        fflush(stdout);
        exit(1);
    }

    static void satie_error(satie_auxil_t* auxil) {
        panic("Syntax error near line %d", ROW);
    }
}

%auxil "satie_auxil_t*"
%value "ast_node_t*"

%source {
#define PCC_GETCHAR(auxil) satie_getchar(auxil)
#define PCC_ERROR(auxil) satie_error(auxil)
}

#
# Top level structure
#

Program <- _ (i:Imports _)? tld:TopLevelDefs? EOF { $$ = CN(PROGRAM, 2, i, tld); }
TopLevelDefs <- tld:TopLevelDef { $$ = CN(TOP_LEVEL_DEFS, 1, tld); } (_ tld:TopLevelDef { AC($$, tld); })*
TopLevelDef <- (tld:AliasDef /
                tld:TypeDef /
                tld:EnumDef /
                tld:FunctionDef /
                tld:InterfaceDef /
                tld:RecordDef /
                tld:TaskDef) (_ UnexpectedSeparator)? { $$ = tld; }

#
# Import
#

Imports <- i:Import { $$ = CN(IMPORTS, 1, i); } (__ i:Import { AC($$, i); })*
Import <- "import" __ (ma:ModuleAlias _ "=" _)? mc:Module (_ ":" _ in:ImportedDefs)? (_ UnexpectedSeparator)? { $$ = CN(IMPORT, 3, ma, mc, in); }
ModuleAlias <- Identifier { $$ = CT(MODULE_ALIAS, $0); }
Module <- n:Name {$$ = CN(MODULE, 1, n); } ( "." n:Name { AC($$, n); })*
ImportedDefs <- n:Name { $$ = CN(IMPORTED_DEFS, 1, n); } (_ "," _ n:Name { AC($$, n); })*

#
# Alias definition
#

AliasDef <- "alias" __ n:Name ("<" _ tv:TypeVariables _ ">")? _ "=" _ t:Type { $$ = CN(ALIAS_DEF, 3, n, tv, t); }

#
# Type definition
#

TypeDef <- "type" __ n:Name ("<" _ tv:TypeVariables _ ">")? _ "=" _ c:Constructors { $$ = CN(TYPE_DEF, 3, n, tv, c); }
Constructors <- c:Constructor { $$ = CN(CONSTRUCTORS, 1, c); } (__ "or" __ c:Constructor { AC($$, c); })*
Constructor <- n:Name ("<" _ t:Types? _ ">")? { $$ = CN(CONSTRUCTOR, 2, n, t); }

#
# Enumeration definition
#

EnumDef <- "enum" __ n:Name (_ ":" _ t:Type)? _ "{" _ e:Enums _ "}" { $$ = CN(ENUM_DEF, 3, n, CCN(TYPE, 1, t), e); }
Enums <- e:Enum { $$ = CN(ENUMS, 1, e); } (_ UnexpectedSeparator)? (_ e:Enum { AC($$, e); } (_ UnexpectedSeparator)?)*
Enum <- n:Name (_ "=" _ e:Expr)? { $$ = CN(ENUM, 2, n, CCN(ENUM_VALUE, 1, e)); }

#
# Function definition
#

FunctionDef <- e:Export? _ "fn" __ fn:FunctionName _ ("<" _ tv:TypeVariables _ ">" _)? "(" _ p:Params? _ ")" (_ "->" _ t:Type _)? _ be:BlockExpr { $$ = CN(FUNCTION_DEF, 6, e, fn, tv, p, CCN(TYPE, 1, t), be); }
Export <- "export" { $$ = CT(EXPORT, NULL); }
FunctionName <- Identifier { $$ = CT(FUNCTION_NAME, $0); }
Params <- p:Param { $$ = CN(PARAMS, 1, p); } (_ "," _ p:Param { AC($$, p); })*
Param <- n:Name (_ ":" _ t:Type)? { $$ = RN(n, PARAM_NAME); AC($$, CCN(TYPE, 1, t));}

#
# Interface definition
#

InterfaceDef <- "interface" __ n:Name ("<" _ tv:TypeVariables _ ">")? _ "{" _
                 im:InterfaceMembers _
                 "}" { $$ = CN(INTERFACE_DEF, 3, n, tv, im); }
InterfaceMembers <- im:InterfaceMember (_ UnexpectedSeparator)? { $$ = CN(INTERFACE_MEMBERS, 1, im); } (_ im:InterfaceMember { AC($$, im); } (_ UnexpectedSeparator)?)*
InterfaceMember <- (im:InterfaceMemberMethod / i:InterfaceMemberProperty) { $$ = im; }
InterfaceMemberMethod <- ma:MemberAccess __ im:InterfaceMethod { $$ = CN(INTERFACE_MEMBER_METHOD, 2, ma, im); }
InterfaceMethod <- "fn" __ fn:FunctionName ("<" _ tv:TypeVariables _ ">")? _ "(" _ p:Params? _ ")" (_ "->" _ t:Type)? _ { $$ = CN(INTERFACE_METHOD, 4, fn, tv, p, CCN(TYPE, 1, t)); }
InterfaceMemberProperty <- (ma:MemberAccess (_ c:Const)? / c:Readonly) __ n:Name (_ ":" _ t:Type { AC(n, CCN(TYPE, 1, t)); })? { $$ = CN(INTERFACE_MEMBER_PROPERTY, 4, ma, c, n, t); }

#
# Record definition
#

RecordDef <- "record" __ n:Name ("<" _ tv:TypeVariables _ ">")? (__ "implements" __ i:Interfaces )? _ "{" _
             rm:RecordMembers _
             "}" { $$ = CN(RECORD_DEF, 4, n, tv, i, rm); }
Interfaces <- i:Interface { $$ = CN(INTERFACES, 1, i); } (_ "," _ i:Interface { AC($$, i); })*
Interface <- n:Name ("<" _ t:Types _ ">")? { $$ = CN(INTERFACE, 2, n, t); }
RecordMembers <- rm:RecordMember (_ UnexpectedSeparator)? { $$ = CN(RECORD_MEMBERS, 1, rm); } (_ rm:RecordMember { AC($$, rm); } (_ UnexpectedSeparator)?)*
RecordMember <- (rm:MemberMethod / rm:MemberProperty) { $$ = rm; }
MemberMethod <- ma:MemberAccess __ fd:FunctionDef { $$ = CN(MEMBER_METHOD, 2, ma, fd); }
MemberAccess <- "public" { $$ = CT(PUBLIC, NULL); } / "private" { $$ = CT(PRIVATE, NULL); }
MemberProperty <- (ma:MemberAccess (_ c:Const)? / c:Readonly) __ n:Name (_ ":" _ t:Type { AC(n, CCN(TYPE, 1, t)); })? (_ "=" _ e:Expr)? { $$ = CN(MEMBER_PROPERTY, 4, ma, c, n, e); }
Const <- "const" { $$ = CT(CONST, NULL); }
Readonly <- "readonly" { $$ = CT(READONLY, NULL); }

#
# Task definition
#

TaskDef <- "task" __ n:Name ("<" _ tv:TypeVariables _ ">")? (__ "implements" __ i:Interfaces)? _ "{" _
           rm:RecordMembers _
           "}" { $$ = CN(TASK_DEF, 4, n, tv, i, rm); }

#
# Type
#

Type <- (t:BasicType / t:ListType / t:FunctionType / t:TupleType / t:MapType / t:ConstructorType / t:TypeVariable / t:Name) { $$ = t; }
BasicType <- (b:BoolType / b:IntType / b:FloatType / b:CharType / b:StringType / b:TaskType) { $$ = b;}
BoolType <- "Bool" { $$ = CT(BOOL_TYPE, NULL); }
IntType <- "Int" { $$ = CT(INT_TYPE, NULL); }
FloatType <- "Float" { $$ = CT(FLOAT_TYPE, NULL); }
CharType <- "Char" { $$ = CT(CHAR_TYPE, NULL); }
StringType <- "String" { $$ = CT(STRING_TYPE, NULL); }
TaskType <- "Task" { $$ = CT(TASK_TYPE, NULL); }
ListType <- "[" _ t:Type _ "]" {$$ = CN(LIST_TYPE, 1, t); } / "[" _ "]" { $$ = CT(EMPTY_LIST_TYPE, NULL); }
FunctionType <- "(" _ ("<" _ tv:TypeVariables _ ">" _)? a:Type _ "->" _ r:Type _ ")" { $$ = CN(FUNCTION_TYPE, 3, tv, CCN(ARG_TYPES, 1, a), r); } / "(" _ ("<" _ tv:TypeVariables _ ">" _)? "[" _ at:ArgTypes? _ "]" _ "->" _ r:Type _ ")" { $$ = CN(FUNCTION_TYPE, 3, tv, at, r); }
ArgTypes <- a:Type { $$ = CN(ARG_TYPES, 1, a); } (_ "," _ a:Type { AC($$, a); })*
TupleType <- "(" _ t:Type { $$ = CN(TUPLE_TYPE, 1, t); } (_ "," _ t:Type { AC($$, t); })* _ ")" / "(" _ ")" { $$ = CT(EMPTY_TUPLE_TYPE, NULL); }
MapType <- "[" _ k:Type _ ":" _ v:Type _ "]" { $$ = CN(MAP_TYPE, 2, k, v); } / "[" _ ":" _ "]" { $$ = CT(EMPTY_MAP_TYPE, NULL); }

ConstructorType <- n:Name "<" _ t:Types? _ ">" { $$ = CN(CONSTRUCTOR_TYPE, 2, n, t); }

Types <- t:Type { $$ = CN(TYPES, 1, CN(TYPE, 1, t)); } (_ "," _ t:Type { AC($$, CN(TYPE, 1, t)); })*

TypeVariables <- t:TypeVariable {$$ = CN(TYPE_VARIABLES, 1, t); } (_ "," _ t:TypeVariable { AC($$, t); })*
TypeVariable <- "$" Identifier { $$ = CT(TYPE_VARIABLE, $0); }

#
# Expression
#

Exprs <- e:Expr { $$ = CN(EXPRS, 1, e); } (_ "," _ e:Expr { AC($$, e); })*

Expr <- "this" _ "(" _ (":" / a:NamedArgs) _ ")" { $$ = CN(THIS, 1, a); } / e:BindExpr { $$ = e; }

BindExpr <- l:Expr (_ ":" _ t:Type)? (__ "as" __ un:UnboundName)? _ ":=" _ r:Expr { $$ = CN(BIND, 4, l, CCN(IS, 1, t), CCN(AS, 1, un), r); } / e:LogicalOrExpr { $$ = e; }
LogicalOrExpr <- l:LogicalOrExpr _ "||" _ r:LogicalAndExpr { $$ = CN(OR, 2, l, r); } / e:LogicalAndExpr { $$ = e; }
LogicalAndExpr <- l:LogicalAndExpr _ "&&" _ r:BitwiseAndExpr { $$ = CN(AND, 2, l, r); } / e:BitwiseAndExpr { $$ = e; }
BitwiseAndExpr <- l:BitwiseAndExpr _ "&" _ r:BitwiseOrExpr { $$ = CN(BITWISE_AND, 2, l, r); } / e:BitwiseOrExpr { $$ = e; }
BitwiseOrExpr <- l:BitwiseOrExpr _ "|" _ r:GTEIntExpr { $$ = CN(BITWISE_OR, 2, l, r); } / e:GTEIntExpr { $$ = e; }
GTEIntExpr <- l:GTEIntExpr __ ">=" __ r:GTEFloatExpr { $$ = CN(GTE_INT, 2, l, r); } / e:GTEFloatExpr { $$ = e; }
GTEFloatExpr <- l:GTEFloatExpr __ ">=." __ r:GTIntExpr { $$ = CN(GTE_FLOAT, 2, l, r); } / e:GTIntExpr { $$ = e; }
GTIntExpr <- l:GTIntExpr __ ">" __ r:GTFloatExpr { $$ = CN(GT_INT, 2, l, r); } / e:GTFloatExpr { $$ = e; }
GTFloatExpr <- l:GTFloatExpr __ ">." __ r:LTEIntExpr { $$ = CN(GT_FLOAT, 2, l, r); } / e:LTEIntExpr { $$ = e; }
LTEIntExpr <- l:LTEIntExpr __ "<=" __ r:LTEFloatExpr { $$ = CN(LTE_INT, 2, l, r); } / e:LTEFloatExpr { $$ = e; }
LTEFloatExpr <- l:LTEFloatExpr __ "<=." __ r:LTIntExpr { $$ = CN(LTE_FLOAT, 2, l, r); } / e:LTIntExpr { $$ = e; }
LTIntExpr <- l:LTIntExpr __ "<" __ r:LTFloatExpr { $$ = CN(LT_INT, 2, l, r); } / e:LTFloatExpr { $$ = e; }
LTFloatExpr <- l:LTFloatExpr __ "<." __ r:NotEqualExpr { $$ = CN(LT_FLOAT, 2, l, r); } / e:NotEqualExpr { $$ = e; }
NotEqualIntExpr <- l:NotEqualIntExpr _ "!=" _ r:NotEqualFloatExpr { $$ = CN(NE, 3, l, CT(INT_TYPE, NULL), r); } / e:NotEqualFloatExpr { $$ = e; }
NotEqualFloatExpr <- l:NotEqualFloatExpr _ "!=." _ r:NotEqualExpr { $$ = CN(NE, 3, l, CT(FLOAT_TYPE, NULL), r); } / e:NotEqualExpr { $$ = e; }
NotEqualExpr <- l:NotEqualExpr _ "!" !("Int" / "Float") t:Name "=" _ r:EqualIntExpr { $$ = CN(NE, 3, l, t, r); } / e:EqualIntExpr { $$ = e; }
EqualIntExpr <- l:EqualIntExpr _ "==" _ r:EqualFloatExpr { $$ = CN(EQ, 3, l, CT(INT_TYPE, NULL), r); } / e:EqualFloatExpr { $$ = e; }
EqualFloatExpr <- l:EqualFloatExpr _ "==." _ r:EqualExpr { $$ = CN(EQ, 3, l, CT(FLOAT_TYPE, NULL), r); } / e:EqualExpr { $$ = e; }
EqualExpr <- l:EqualExpr _ "=" !("Int" / "Float") t:Name "=" _ r:RightShiftExpr { $$ = CN(EQ, 3, l, RN(t, EQ_TYPE), r); } / e:RightShiftExpr { $$ = e; }
RightShiftExpr <- l:RightShiftExpr __ ">>" __ e:LeftShiftExpr { $$ = CN(BSR, 2, l, e); } / e:LeftShiftExpr { $$ = e; }
LeftShiftExpr <- l:LeftShiftExpr __ "<<" __ e:ConsExpr { $$ = CN(BSL, 2, l, e); } / e:ConsExpr { $$ = e; }
ConsExpr <- l:ConsExpr _ "::" _ r:ListConcatExpr { $$ = CN(CONS, 2, l, r); } / e:ListConcatExpr { $$ = e; }
ListConcatExpr <- l:ListConcatExpr _ "@" _ r:MapConcatExpr { $$ = CN(CONCAT_LIST, 2, l, r); } / e:MapConcatExpr { $$ = e; }
MapConcatExpr <- l:MapConcatExpr _ "~" _ r:StringConcatExpr { $$ = CN(CONCAT_MAP, 2, l, r); } / e:StringConcatExpr { $$ = e; }
StringConcatExpr <- l:StringConcatExpr _ "^" _ r:SubIntExpr { $$ = CN(CONCAT_STRING, 2, l, r); } / e:SubIntExpr { $$ = e; }
SubIntExpr <- l:SubIntExpr _ "-" _ r:SubFloatExpr { $$ = CN(SUB_INT, 2, l, r); } / e:SubFloatExpr { $$ = e; }
SubFloatExpr <- l:SubFloatExpr _ "-." _ r:AddIntExpr { $$ = CN(SUB_FLOAT, 2, l, r); } / e:AddIntExpr { $$ = e; }
AddIntExpr <- l:AddIntExpr _ "+" _ r:AddFloatExpr { $$ = CN(ADD_INT, 2, l, r); } / e:AddFloatExpr { $$ = e; }
AddFloatExpr <- l:AddFloatExpr _ "+." _ r:ModExpr { $$ = CN(ADD_FLOAT, 2, l, r); } / e:ModExpr { $$ = e; }
ModExpr <- l:ModExpr _ "%" _ r:DivIntExpr { $$ = CN(MOD, 2, l, r); } / e:DivIntExpr { $$ = e; }
DivIntExpr <- l:DivIntExpr _ "/" _ r:DivFloatExpr { $$ = CN(DIV_INT, 2, l, r); } / e:DivFloatExpr { $$ = e; }
DivFloatExpr <- l:DivFloatExpr _ "/." _ r:MulIntExpr { $$ = CN(DIV_FLOAT, 2, l, r); } / e:MulIntExpr { $$ = e; }
MulIntExpr <- l:MulIntExpr _ "*" _ r:MulFloatExpr { $$ = CN(MUL_INT, 2, l, r); } / e:MulFloatExpr { $$ = e; }
MulFloatExpr <- l:MulFloatExpr _ "*." _ r:ExpExpr { $$ = CN(MUL_FLOAT, 2, l, r); } / e:ExpExpr { $$ = e; }
ExpExpr <- l:ExpExpr _ "^^" _ r:NotExpr { $$ = CN(EXP, 2, l, r); } / e:NotExpr { $$ = e; }
NotExpr <- "!" _ l:PosIntExpr { $$ = CN(NOT, 1, l); } / e:PosIntExpr { $$ = e; }
PosIntExpr <- "+" _ l:PosFloatExpr { $$ = CN(POS_INT, 1, l); } / e:PosFloatExpr { $$ = e; }
PosFloatExpr <- "+." _ l:NegIntExpr { $$ = CN(POS_FLOAT, 1, l); } / e:NegIntExpr { $$ = e; }
NegIntExpr <- "-" _ l:NegFloatExpr { $$ = CN(NEG_INT, 1, l); } / e:NegFloatExpr { $$ = e; }
NegFloatExpr <- "-." _ l:PostfixExpr { $$ = CN(NEG_FLOAT, 1, l); } / e:PostfixExpr { $$ = e; }
PostfixExpr <- (p:PrimaryExpr { $$ = CN(POSTFIX, 1, p); }
                (_ "<~" _ n:Name { AC($$, RN(n, TASK_CALL)); } /
                 _ "~>" _ n:Name { AC($$, RN(n, TASK_CAST)); } /
                 _ "." _ n:Name { AC($$, RN(n, RECORD_DOT)); } /
                 _ "[" _ mkv:MapKeyValues _ "]" { AC($$, RN(mkv, MAP_UPDATE)); } /
                 _ "[" _ ":" _ e:Expr _ "]" { AC($$, CN(MAP_LOOKUP, 1, e)); } /
                 _ "[" _ s:Expr _ ".." _ e:Expr _ "]" { AC($$, CN(LIST_SLICE, 2, s, e)); } /
                 _ "[" _ iv:IndexValues _ "]" { AC($$, iv); } /
                 _ "[" _ e:Expr _ "]" { AC($$, CN(LIST_LOOKUP, 1, e)); } /
                 _ ("<" _ t:Types _ ">")? "(" _ a:Args? _ ")" { AC($$, CN(FUNCTION_CALL, 2, t, a)); }
                )*
               )

IndexValues <- iv:IndexValue { $$ = CN(LIST_UPDATE, 1, iv); } (_ "," _ iv:IndexValue { AC($$, iv); })*
IndexValue <- i:Expr _ "=" _ v:Expr { $$ = CN(INDEX_VALUE, 2, i, v); }

PrimaryExpr <- "this" { $$ = CT(THIS, NULL); } /
               "$" { $$ = CT(SLICE_LENGTH, NULL); } /
               pe:NewRecord {$$ = pe;} /
               pe:LaunchTask {$$ = pe;} /
               pe:ControlFlowExpr { $$ = pe; } /
               pe:Literal { $$ = pe; } /
               pe:UnboundName { $$ = pe; } /
               pe:Name { $$ = pe; } /
               "(" _ e:Expr _ ")" { $$ = e; }

NewRecord <- n:Name _ ("<" _ t:Types _ ">" _)? "(" _ (":" / na:NamedArgs) _ ")" { $$ = CN(NEW_RECORD, 3, n, t, na); }

NamedArgs <- na:NamedArg { $$ = CN(NAMED_ARGS, 1, na); } (_ "," _ na:NamedArg { AC($$, na); })*
NamedArg <- nan:NamedArgName _ ":" _ e:Expr { $$ = CN(NAMED_ARG, 2, nan, e); } /
            un:UnboundName _ ":" { $$ = CN(NAMED_ARG, 2, RN(un, NAMED_ARG_NAME), un); }
NamedArgName <- Identifier { $$ = CT(NAMED_ARG_NAME, $0); }

LaunchTask <- "launch" __ n:Name _ ("<" _ t:Types _ ">" _)? "(" _ (":" / na:NamedArgs) _ ")" { $$ = CN(LAUNCH_TASK, 3, n, t, na); }

ControlFlowExpr <- (cfe:IfExpr / cfe:SwitchExpr / cfe:BlockExpr) { $$ = cfe; }

IfExpr <- "if" __ e:Expr _ be:BlockExpr { $$ = CN(IF, 2, e, be); }
          (_ "elif" __ e:Expr _ be:BlockExpr { AC($$, CN(ELIF, 2, e, be)); })*
          (_ "else" _ be:BlockExpr { AC($$, CN(ELSE, 1, be)); })

SwitchExpr <- "switch" __ se:Expr { $$ = CN(SWITCH, 1, se); } _ "{"
              (_ "case" __ me:Exprs (_ ":" _ t:Type)? (__ "as" __ un:UnboundName)? (__ "when" __ we:Expr)? _ "=>" _ b:Block { AC($$, CN(CASE, 5, me, CCN(AS, 1, un), CCN(IS, 1, t), CCN(WHEN, 1, we), b)); })+
              (_ "default" _ "=>" _ b:Block { AC($$, CN(DEFAULT, 1, b)); })? _
              "}"

BlockExpr <- "{" _ be:Block _ "}" { $$ = be; }
Block <- e:Expr { $$ = CN(BLOCK, 1, e); } ( _ CommaSeparator _ e:Expr { AC($$, e); })*
CommaSeparator <- ("," / ";") {
    if (strcmp($0, ";") == 0) {
        panic("%d: Use ',' on to separate expressions in blocks", auxil->row);
    }
}

Literal <- (l:BasicLiteral / l:CompositeLiteral) { $$ = l; }

BasicLiteral <- (bl:BoolLiteral /
                 bl:NumberLiteral /
                 bl:CharLiteral /
                 bl:StringLiteral /
                 bl:FunctionLiteral /
                 bl:EnumLiteral) { $$ = bl; }

CompositeLiteral <- (cl:TupleLiteral /
                     cl:ListLiteral /
                     cl:MapLiteral /
                     cl:ConstructorLiteral) { $$ = cl; }

BoolLiteral <- "true" { $$ = CT(TRUE, NULL); } / "false" { $$ = CT(FALSE, NULL); }

NumberLiteral <- (nl:Float / nl:Int) { $$ = nl; }

Float <- (f:DecimalPointLeading / f:DecimalPointTrailing) { $$ = CT(FLOAT, $0); }
DecimalPointLeading <- [0-9]+ "." [0-9]+ ExponentPart?
DecimalPointTrailing <- [0-9]+ ExponentPart
ExponentPart <- [eE] [+-]? [0-9]+

Int <- (HexInt / BinaryInt / OctalInt / DecimalInt) { $$ = CT(INT, $0); }
HexInt <- "0x" [0-9a-fA-F]+
BinaryInt <- "0b" [01]+
OctalInt <- "0" [0-7]+
DecimalInt <- [0-9]+

CharLiteral <- "'" (c:EscapeChar / c:NonQuoteChar) "'" { $$ = CN(CHAR, 1, c); }
EscapeChar <- "\\" (
              [abfnrtv'"\\] /
              "x" HexDigit HexDigit /
              "u" HexDigit HexDigit HexDigit HexDigit /
              "U" HexDigit HexDigit HexDigit HexDigit
                  HexDigit HexDigit HexDigit HexDigit /
               OctalDigit /
               OctalDigit OctalDigit /
               OctalDigit OctalDigit OctalDigit
               ) { $$ = CT(ESCAPE_CHAR, $0); }
HexDigit <- [0-9a-fA-F]
OctalDigit <- [0-7]
NonQuoteChar <- [^'] { $$ = CT(NON_QUOTE_CHAR, $0); }

StringLiteral <- ('"' s:RegularString '"' / 'r"' s:RawString '"') { $$ = s; }
RegularString <- (EscapeSequence / [^"])* { $$ = CT(REGULAR_STRING, $0); }
EscapeSequence <- "\\" [btnvfr"\\]
RawString <- [^"]* { $$ = CT(RAW_STRING, $0); }

FunctionLiteral <- "fn" __ f:FunctionName? _ ("<" _ tv:TypeVariables _ ">" _)? "(" _ p:Params? _ ")" (_ "->" _ Type)? _ e:BlockExpr { $$ = CN(FUNCTION_LITERAL, 4, f, tv, p, e); }

EnumLiteral <- en:Name _ "#" _ n:Name { $$ = CN(ENUM_LITERAL, 2, en, n); }

TupleLiteral <- "(" _ ")" { $$ = CT(TUPLE_LITERAL, NULL); } /
                "(" _ e:Expr _ "," _ ")" { $$ = CN(TUPLE_LITERAL, 1, e); } /
                "(" _ te:TupleExprs _ ")" { $$ = te; }
TupleExprs <- e1:Expr _ "," _ e2:Expr { $$ = CN(TUPLE_LITERAL, 2, e1, e2); } (_ "," _ e:Expr { AC($$, e); })*

ListLiteral <- "[" _ "]" { $$ = CT(LIST_LITERAL, NULL); } / "[" _ e:Exprs? _ "]" { $$ = RN(e, LIST_LITERAL); }

MapLiteral <- "[" _ ":" _ "]" { $$ = CT(MAP_LITERAL, NULL); } / "[" _ k:MapKeyValues? _ "]" { $$ = RN(k, MAP_LITERAL); }
MapKeyValues <- m:MapKeyValue { $$ = CN(MAP_KEY_VALUES, 1, m); } (_ "," _ m:MapKeyValue { AC($$, m); })*
MapKeyValue <- k:Expr _ ":" _ v:Expr { $$ = CN(MAP_KEY_VALUE, 2, k, v); }

ConstructorLiteral <- (Name _ ";" _)? n:Name "<" _ e:Exprs? _ ">" !(_"(") { $$ = CN(CONSTRUCTOR_LITERAL, 2, n, e); }

Args <- e:Expr { $$ = CN(ARGS, 1, e); } (_ "," _ e:Expr { AC($$, e); })*

#
# Misc
#

Reserved <- ("alias" /
             "as" /
             "case" /
             "const" /
             "default" /
             "elif" /
             "else" /
             "enum" /
             "export" /
             "false" /
             "fn" /
             "if" /
             "import" /
             "interface" /
             "launch" /
             "private" /
             "public" /
             "readonly" /
             "record" /
             "implements"
             "switch" /
             "task" /
             "this" /
             "true" /
             "type" /
             "when") ![a-zA-Z_0-9_]
Identifier <- !Reserved [a-zA-Z_][a-zA-Z_0-9_]*
Name <- Identifier { $$ = CT(NAME, $0); }
UnboundName <- "?" n:Name { $$ = RN(n, UNBOUND_NAME); } / "_" { $$ = CT(UNBOUND_NO_NAME, $0); }

#_ <- WS*
#__ <- WS+
_ <- (WS / Comments)*
__ <- (WS / Comments)+
WS <- [ \t\r\n] {
    if ($0[0] == '\n') {
        auxil->row++;
    }
}
Comments <- SingleLineComment / BlockComment
SingleLineComment <- "//" (!EOL .)* EOL?
EOL <- ("\r\n" / "\n" / "\r") { auxil->row++; }
BlockComment <- "/*" (BlockCommentContent / EOL)* "*/"
BlockCommentContent <- (!("*/" / EOL) .)
EOF <- _ !.

UnexpectedSeparator <- ("," / ";") {
    panic("%d: Do not use '%s' as a separator", auxil->row, $0);
}

%%
int main() {
#ifdef UNITTEST
    symbol_table_unit_test();
#endif
    /*
     * Parse source
     */
    satie_auxil_t* satie_auxil = satie_auxil_new();
    satie_context_t *context = satie_create(satie_auxil);
    ast_node_t* program;
    satie_parse(context, &program);
    //ast_print(program, 0);
    /*
     * Check semantics
     */
    satie_error_t error;
    if (!hm_infer_types(program, &error)) {
       fprintf(stderr, "Error: %s\n", satie_error_message);
       return 1;
    }
    /*
     * Cleanup
     */
    satie_destroy(context);
    return 0;
}
