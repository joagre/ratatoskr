A Plain Old Stack Machine (POSM)
================================

This document describes a Plain Old Stack Machine (POSM) intended to
be used in experiments with different types of thread scheduling
algorithms. POSM sports the following instructions: PUSH, PUSHS, POP,
DUP, SWAP, LOAD, STORE, ADD, SUB, MUL, DIV, JUMP, CJUMP, CALL, RET,
SYSCALL, AND, OR, NOT, EQ, NEQ, LT, GT, NOP and HALT.

POSM also has two read-only registers:

SP: Stack pointer
FP: Frame pointer

NOTE: The stack grows downwards.

Instruction set
===============

LABEL label       ; A jump label. This is a meta instruction.
PUSH value        ; Pushes a value onto the stack.
PUSHS string      ; Pushes a string value onto the stack.
POP               ; Removes the top value from the stack.
DUP               ; Duplicates the top value on the stack.
SWAP              ; Swaps the top two values on the stack.
LOAD SP|FP        ; Copies a value on the stack to the top of the stack.
                  ; The top value of the stack must be an offset to SP
                  ; or FP
STORE SP|FP       ; Updates a value on the stack. The top value of the
                  ; stack must an offset to the SP or FP and the new
                  ; value comes next.
ADD               ; Pops two values, adds them, and pushes the result.
SUB               ; Pops two values, subtracts them, and pushes the result.
MUL               ; Pops two values, multiplies them, and pushes the result.
DIV               ; Pops two values, divides them, and pushes the result.
JUMP label        ; Unconditionally jumps to a label.
CJUMP label       ; Conditionally jumps based to a label if the top
                  ; value of the stack is not 0. Pops the top value.
CALL label arity  ; Calls a function implemented in label with the
                  ; specified arity. Pushes the return address and
                  ; previous FP onto the stack. Sets FP to the first
                  ; parameter on the stack.
RET               ; Removes the stack frame as specified by FP
                  ; (everything below FP is nuked). Restores FP to the
                  ; previous FP. Pushes the return value onto the stack and
                  ; jumps to the return address.
SYS name          ; Calls a built-in function, e.g. spawn, send, recv,
                  ; rand, sleep and println etc.
AND               ; Pops two values, performs a logical AND, and
                  ; pushes the result.
OR                ; Pops two values, performs a logical OR, and
                  ; pushes the result.
NOT               ; Pops two values, performs a logical NOT, and
                  ; pushes the result.
EQ                ; Pops two values, checks if they are equal, and
                  ; pushes the result (0 or 1).
NEQ               ; Pops two values, checks if they are not equal, and
                  ; pushes the result (0 or 1).
LT                ; Pops two values, checks if the first is less than
                  ; the second, and pushes the result (0 or 1).
GT                ; Pops two values, checks if the first is greater than
                  ; the second, and pushes the result (0 or 1).
NOP               ; No operation. Do nothing.
HALT              ; Halts the stack machine execution.

Example 1
=========

Consider this Erlang snippet:

start() ->
    summer(13, 10).

summer(_M, 0) ->
    0;
summer(M, N) ->
    M + summer(M, N - 1).

This is how it could be written:

__EXAMPLE1_START__
LABEL 0          ; start/0
  PUSH -1        ; Bogus return address
  PUSH -1        ; Bogus FP
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ; Call summer(13, 10)
  PUSH 13        ; Parameter M
  PUSH 10        ; Parameter N
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   13
  ;;   10
  CALL 1 2
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   130 (Return value)
  RET
  ;; Stack:
  ;;   130

LABEL 1          ; summer/2
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Return address
  ;;   Previous FP
  ; Is it summer(_M, 0) clause?
  PUSH -1
  LOAD FP       ; Load parameter N
  PUSH 0
  NEQ
  CJUMP 2
  PUSH 0
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Return address
  ;;   Previous FP
  ;;   0
  RET
  ;; Stack:
  ;;   0

LABEL 2
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Return address
  ;;   Previous FP
  ; It is summer(M, N) clause!
  ; Call summer(M, N - 1)
  PUSH 0
  LOAD FP       ; Load parameter M
  PUSH 0
  LOAD FP       ; Parameter M
  PUSH -1
  LOAD FP       ; Load parameter N
  PUSH 1
  SUB           ; Parameter N - 1
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Return address
  ;;   Previous FP
  ;;   M
  ;;   N - 1
  CALL 1 2
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Return address
  ;;   Previous FP
  ;;   summer(M, N - 1) (Return value)
  ADD           ; M + summer(M, N - 1)
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Return address
  ;;   Previous FP
  ;;   M + summer(M, N - 1)
  RET
  ;; Stack:
  ;;   M + summer(M, N - 1)
__EXAMPLE1_END__

Example 2
=========

Consider this Erlang snippet:

start() ->
    summer(13, 10, 0).

summer(_M, 0, Acc) ->
    Acc;
summer(M, N, Acc) ->
    summer(M, N - 1, Acc + M).

It does the same as the example 1 but with an accumulated
parameter. This makes the function tail recursive, i.e. the stack
frame can be reused for each recursion and the call stack will not
grow. To achieve this a CALL is not used. Instead parameters are
patched in the current stack frame and then a plain JUMP is done to
perform next iteration.

This is how it could be written:

__EXAMPLE2_START__
LABEL 0          ; start/0
  PUSH -1        ; Bogus return address
  PUSH -1        ; Bogus FP
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ; Call summer(13, 10)
  PUSH 13       ; Parameter M
  PUSH 10       ; Parameter N
  PUSH 0        ; Parameter Acc
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   13
  ;;   10
  ;;   0
  CALL 1 3
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   130 (Return value)
  RET
  ;; Stack:
  ;;   130

LABEL 1          ; summer/2
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Acc
  ;;   Return address
  ;;   Previous FP
  ; Is it summer(_M, 0, Acc) clause?
  PUSH -1
  LOAD FP        ; Load parameter N
  PUSH 0
  NEQ
  CJUMP 2
  PUSH -2
  LOAD FP        ; Load parameter Acc
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Acc
  ;;   Return address
  ;;   Previous FP
  ;;   Acc
  RET
  ;; Stack:
  ;;   Acc

LABEL 2
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Acc
  ;;   Return address
  ;;   Previous FP
  ; It is summer(M, N, Acc)!
  ; Call summer(M, N - 1, Acc + M)
  PUSH -1
  LOAD FP        ; Load parameter N
  PUSH 1
  SUB            ; N - 1
  PUSH -2
  LOAD FP        ; Load parameter Acc
  PUSH 0
  LOAD FP        ; Load parameter M
  ADD
  ;; Stack:
  ;;   M <- FP
  ;;   N
  ;;   Acc
  ;;   Return address
  ;;   Previous FP
  ;;   N - 1
  ;;   Acc + M
  ; Patch (changed) parameters in place and jump to label 1
  PUSH -2
  STORE FP      ; Patches parameter Acc -> Acc + M
  PUSH -1
  STORE FP      ; Patches parameter N -> N - 1
  ;; Stack:
  ;;   M <- FP
  ;;   N - 1
  ;;   Acc + M
  ;;   Return address
  ;;   Previous FP
  JUMP 1         ; Voila! The old stack frame is reused.
__EXAMPLE2_END__

Example 3
=========

Consider this Erlang snippet:

start() ->
    X = 1,
    Y = "foo",
    Z = X + 1,
    bar(Y, Z).

bar(Y, Z) ->
    io:format("~p\n", [Z]),
    Y.

This rather silly example introduces how to deal with local variables,
string literals and SYS calls.

__EXAMPLE3_START__
LABEL 0          ; start/0
  PUSH -1        ; Bogus return address
  PUSH -1        ; Bogus FP
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  PUSH 1         ; Local variable X
  PUSHS "foo"    ; Local variable Y
  PUSH -2
  LOAD FP
  PUSH 1
  ADD            ; Local variable Z
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   1 (Local variable X)
  ;;   "foo" (Local variable Y)
  ;;   2 (Local variable Z)
  ; Call bar(Y, Z)
  PUSH -3
  LOAD FP        ; Parameter Y
  PUSH -4
  LOAD FP        ; Parameter Z
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   1 (Local variable X)
  ;;   "foo" (Local variable Y)
  ;;   2 (Local variable Z)
  ;;   "foo" (Local variable Y)
  ;;   2 (Local variable Z)
  CALL 1 2
  ;; Stack:
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   1 (Local variable X)
  ;;   "foo" (Local variable Y)
  ;;   2 (Local variable Z)
  ;;   "foo" (Local variable Y) (Return value)
  RET
  ;; Stack:
  ;;   "foo" (NOTE: A copy placed on the callers data stack)

LABEL 1          ; bar/2
  ;; Stack:
  ;;   Y <- FP
  ;;   Z
  ;;   Return address <- FP
  ;;   Previous FP
  ; Call SYS println
  PUSH -1
  LOAD FP        ; Parameter Z
  ;; Stack:
  ;;   Y <- FP
  ;;   Z
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   Z
  SYS println
  ;; Stack:
  ;;   Y <- FP
  ;;   Z
  ;;   Return address <- FP
  ;;   Previous FP
  ;;   1 (Return value)
  POP            ; Ignores return bvalue
  PUSH 0
  LOAD FP
  ;; Stack:
  ;;   Y <- FP
  ;;   Z
  ;;   Return address
  ;;   Previous FP
  ;;   Y
  RETC
  ;; Stack:
  ;;   Y
__EXAMPLE3_END__
