import std.jobs : OnCrowding, Job
import std.stdio
import std.lists

fn main() {
  ?jobs = Ackermann.startJobs(3, 10),
  Ackermann.waitForJobs(jobs)
}

singleton struct Ackermann {
    public fn startJobs(m, n, i = 0, jobs = []) {
        if i < n {
            fn computeAckermann(parentJob, m, n) {
                ?result = ackermann(m, n),
                parentJob <- #(self, m, n, result)
            },
            ?job = spawn monitor computeAckermann(self, m, i),
            ?i = i + 1,
            jobs.setMaxMailboxSize(job, 4, OnCrowding.block),
            startJobs(m, n, i, job ~ jobs)
        },
        jobs
    }

    public fn waitForJobs(jobs) {
        if jobs.length > 0 {
            receive {
                case #(?job, ?m, ?n, ?result) {
                    stdio.writeln("ackermann($m, $n) = $result"),
                    waitForJobs(jobs.delete(job))
                }
                case #(Job.died, ?job, ?reason) {
                    stdio.writeln("Oh no! Compute job $job died: $reason")
                }
            }
        }
    }

    private fn ackermann(m, n) {
        if m == 0 {
            n + 1
        } elif n == 0 {
            ackermann(m - 1, 1)
        } else {
            ackermann(m - 1, ackermann(m, n - 1))
        }
    }
}
