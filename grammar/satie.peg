%prefix "satie"

%header {
    #include <stdarg.h>
    #include "satie_auxil.h"
    /*
    static const char *dbg_str[] = { "Evaluating rule", "Matched rule", "Abandoning rule" };
    #define PCC_DEBUG(auxil, event, rule, level, pos, buffer, length) \
    if (strcmp(rule, "WS") != 0 && strcmp(rule, "_") != 0 && strcmp(rule, "__") != 0) \
        fprintf(stderr, "%*s%s %s @%zu [%.*s]\n", (int)((level) * 2), "", dbg_str[event], \
                rule, pos, (int)(length), buffer)
    */
}

%auxil "satie_auxil_t*"
%value "ast_node_t*"

%source {
#define PCC_GETCHAR(auxil) satie_getchar(auxil)
#define PCC_ERROR(auxil) satie_error(auxil)
}

#
# Top level structure
#

Program <- _ (i:Imports __)? t:TopLevelDefs EOF {
    $$ = CCN(PROGRAM, 2, i, t);
}
TopLevelDefs <- t:TopLevelDef { AN(TOP_LEVEL_DEFS, t); } (__ t:TopLevelDef { AN(TOP_LEVEL_DEFS, t); } / TopLevelDefError)* {
    $$ = CSN(TOP_LEVEL_DEFS);
}
TopLevelDef <- ClassDef /
               InterfaceDef /
               t:EnumDef { $$ = t; } /
               t:FunctionDef { $$ = t; }
TopLevelDefError <- ("," / ";") {
    panic("Unexpected %s between top level definitions on line %d",
          $0, auxil->row);
}

#
# Import
#

Imports <- i:Import { AN(IMPORT, i); } (__ i:Import { AN(IMPORT, i); })* {
    $$ = CSN(IMPORTS);
}
Import <- "import" __ (a:Alias _ "=" _)? _ m:Module (_ ":" _ i:ImportedNames)? {
    $$ = CCN(IMPORT, 3, a, m, i);
}
Alias <- Identifier {
    $$ = CT(ALIAS, $0);
}
Module <- m:ModuleComponent {AN(MODULE, m); } ( "." m:ModuleComponent { AN(MODULE, m); })* {
    $$ = CSN(MODULE);
}
ModuleComponent <- Identifier {
    AT(MODULE_COMPONENT, $0);
}
ImportedNames <- m:ImportedName { AN(IMPORTED_NAMES, m); } (_ "," _ m:ImportedName { AN(IMPORTED_NAMES, m); })* {
    $$ = CSN(IMPORTED_NAMES);
}
ImportedName <- Identifier {
    AT(IMPORTED_NAME, $0);
}

#
# Expression
#

Expr <- e:BindExpr { $$ = e; }
BindExpr <- l:MatchExpr _ ":=" _ e:Expr { $$ = CCN(BIND, 2, l, e); } /
            e:LogicalOrExpr { $$ = e; }
LogicalOrExpr <- l:LogicalOrExpr _ "||" _ e:LogicalAndExpr { $$ = CCN(OR, 2, l, e); } /
                 e:LogicalAndExpr { $$ = e; }
LogicalAndExpr <- l:LogicalAndExpr _ "&&" _ e:BitwiseAndExpr { $$ = CCN(AND, 2, l, e); } /
                  e:BitwiseAndExpr { $$ = e; }
BitwiseAndExpr <- l:BitwiseAndExpr _ "&" _ e:BitwiseXorExpr { $$ = CCN(BITWISE_AND, 2, l, e); } /
                  e:BitwiseXorExpr { $$ = e; }
BitwiseXorExpr <- l:BitwiseXorExpr _ "^" _ e:BitwiseOrExpr { $$ = CCN(BITWISE_XOR, 2, l, e); } /
                  e:BitwiseOrExpr { $$ = e; }
BitwiseOrExpr <- l:BitwiseOrExpr _ "|" _ e:GreaterThanEqualExpr { $$ = CCN(BITWISE_OR, 2, l, e); } /
                 e:GreaterThanEqualExpr { $$ = e; }
GreaterThanEqualExpr <- l:GreaterThanEqualExpr _ "gte" _ e:GreaterThanExpr { $$ = CCN(GTE, 2, l, e); } /
                        e:GreaterThanExpr { $$ = e; }
GreaterThanExpr <- l:GreaterThanExpr _ "gt" _ e:LessThanEqualExpr { $$ = CCN(GT, 2, l, e); } /
                   e:LessThanEqualExpr { $$ = e; }
LessThanEqualExpr <- l:LessThanEqualExpr _ "lte" _ e:LessThanExpr { $$ = CCN(LTE, 2, l, e); } /
                     e:LessThanExpr { $$ = e; }
LessThanExpr <- l:LessThanExpr _ "lt" _ e:NotEqualExpr { $$ = CCN(LT, 2, l, e); } /
                e:NotEqualExpr { $$ = e; }
NotEqualExpr <- l:NotEqualExpr _ "!=" _ e:EqualExpr { $$ = CCN(NE, 2, l, e); } /
                e:EqualExpr { $$ = e; }
EqualExpr <- l:EqualExpr _ "==" _ e:InExpr { $$ = CCN(EQ, 2, l, e); } /
             e:InExpr { $$ = e; }
InExpr <- l:InExpr _ "in" _ e:RightShiftExpr { $$ = CCN(IN, 2, l, e); } /
          e:RightShiftExpr { $$ = e; }
RightShiftExpr <- l:RightShiftExpr _ "bsr" _ e:LeftShiftExpr { $$ = CCN(BSR, 2, l, e); } /
                  e:LeftShiftExpr { $$ = e; }
LeftShiftExpr <- l:LeftShiftExpr _ "bsl" _ e:ConcatExpr { $$ = CCN(BSL, 2, l, e); } /
                 e:ConcatExpr { $$ = e; }
ConcatExpr <- l:ConcatExpr _ "~" _ e:MinusExpr { $$ = CCN(CONCAT, 2, l, e); } /
                   e:MinusExpr { $$ = e; }
MinusExpr <- l:MinusExpr _ "-" _ e:PlusExpr { $$ = CCN(MINUS, 2, l, e); } /
             e:PlusExpr { $$ = e; }
PlusExpr <- l:PlusExpr _ "+" _ e:ModulusExpr { $$ = CCN(PLUS, 2, l, e); } /
            e:ModulusExpr { $$ = e; }
ModulusExpr <- l:ModulusExpr _ "%" _ e:DivideExpr { $$ = CCN(MODULUS, 2, l, e); } /
               e:DivideExpr { $$ = e; }
DivideExpr <- l:DivideExpr _ "/" _ e:MultiplicateExpr { $$ = CCN(DIVIDE, 2, l, e); } /
              e:MultiplicateExpr { $$ = e; }
MultiplicateExpr <- l:MultiplicateExpr _ "*" _ e:ExponentiationExpr { $$ = CCN(MULTIPLY, 2, l, e); } /
                    e:ExponentiationExpr { $$ = e; }
ExponentiationExpr <- l:ExponentiationExpr _ "^^" _ e:CastExpr { $$ = CCN(EXPONENTIATE, 2, l, e); } /
                      e:CastExpr { $$ = e; }
CastExpr <- "cast" _ "(" _ t:Type _ ")" _ e:BitwiseComplementExpr { $$ = CCN(CAST, 2, t, e); } /
            e:BitwiseComplementExpr { $$ = e; }
BitwiseComplementExpr <- "~" _ e:NotExpr { $$ = CCN(BITWISE_COMPLEMENT, 1, e); } /
                         e:NotExpr { $$ = e; }
NotExpr <- "!" _ e:UnaryPlusExpr { $$ = CCN(NOT, 1, e); } /
           e:UnaryPlusExpr { $$ = e; }
UnaryPlusExpr <- "+" _ e:UnaryMinusExpr { $$ = CCN(UNARY_PLUS, 1, e); } /
                 e:UnaryMinusExpr { $$ = e; }
UnaryMinusExpr <- "-" _ e:ListSlice { $$ = CCN(UNARY_MINUS, 1, e); } /
                  e:ListSlice { $$ = e; }
ListSlice <- b:DottedName _ "[" _ s:Expr _ ".." _ e:Expr _ "]" { $$ = CCN(LIST_SLICE, 3, b, s, e); } /
             e:ListUpdate { $$ = e; }
ListUpdate <- b:DottedName _ "[" _ i:IndexValues _ "]" { $$ = CCN(LIST_UPDATE, 2, b, i); } /
              e:MapUpdate { $$ = e; }
MapUpdate <- b:DottedName _ "[" _ m:MapKeyValues _ "]" { $$ = CCN(MAP_UPDATE, 2, b, m); } /
             e:PostfixExpr{ $$ = e; }
PostfixExpr <- (p:PrimaryExpr { AN(POSTFIX_EXPR, p); }
                (_ "." _ b:BoundName { ANA(POSTFIX_EXPR, b, DOT_NAME); } /
                 _ "[" _ e:Expr _ "]" { ANA(POSTFIX_EXPR, e, LIST_LOOKUP); } /
                 _ "(" _ a:Args? _ ")" { ANA(POSTFIX_EXPR, a, FUNCTION_CALL); })*) {
    $$ = CSN(POSTFIX_EXPR);
}

Type <- "int" { $$ = CT(INT_TYPE, NULL); } /
        "float" { $$ = CT(FLOAT_TYPE, NULL); }

DottedName <- b:BoundName { AN(DOTTED_NAME, b); } (_ "." _ b:BoundName { AN(DOTTED_NAME, b); })* {
    $$ = CSN(DOTTED_NAME);
}

IndexValues <- i:IndexValue { AN(INDEX_VALUES, i); } (_ "," _ i:IndexValue { AN(INDEX_VALUES, i); })* {
    $$ = CSN(INDEX_VALUES);
}
IndexValue <- d:DecimalIntegral _ "=" _ e:Expr {
    $$ = CCN(INDEX_VALUE, 2, d, e);
}

PrimaryExpr <- "nil" { $$ = CT(NIL, NULL); } /
               "this" { $$ = CT(THIS, NULL); } /
               "self" { $$ = CT(SELF, NULL); } /
               "$" { $$ = CT(SLICE_LENGTH, NULL); } /
               e:ControlFlowExpr { $$ = e; } /
               e:NewExpr { $$ = e; } /
               e:Literal { $$ = e; } /
               e:BoundName { $$ = e; } /
               "(" _ e:Expr _ ")" { $$ = e; }

ControlFlowExpr <- IfExpr / SwitchExpr / ReceiveExpr / BlockExpr

IfExpr <- "if" __ Expr _ BlockExpr
          (_ "elif" __ Expr _ BlockExpr)*
          (_ "else" _ BlockExpr)?

SwitchExpr <- "switch" __ Expr (_ "is" _ UnboundedName)? _ "{"
             (_ "case" __ MatchExprs _ ":" _ BlockLevelExprs)+ _
             (_ "default" _ ":" _ BlockLevelExprs)? _
             "}"
UnboundedName <- "?" _ Identifier

ReceiveExpr <- "receive" __ Channels (_ "{"
                    (_ "case" __ MatchExprs _ ":" _ BlockLevelExprs _)+
                    (_ "timeout" _ ":" _ DecimalIntegral _ BlockLevelExpr)? _
               "}")?

Channels <- Dereference / "[" _ Dereferences _ "]"
Dereference <- Identifier (_ "." _ Identifier / _ "[" _ Expr _ "]")*
Dereferences <- Dereference (_ "," _ Dereference)*

NewExpr <- "new" _ Identifier _ "(" _ Args? _ ")"

BoundName <- Identifier {
    $$ = CT(BOUND_NAME, $0);
}

#
# Literal
#

Literal <- (l:BaseLiteral / l:CompositeLiteral) {
    $$ = l;
}

BaseLiteral <- (b:BooleanLiteral / b:NumberLiteral / b:CharacterLiteral /
                b:StringLiteral / b:FunctionLiteral / b:EnumLiteral) {
    $$ = b;
}

CompositeLiteral <- (c:TupleLiteral / c:ListLiteral / c:MapLiteral) {
    $$ = c;
}

BooleanLiteral <- "true" { $$ = CT(TRUE, NULL); } /
                  "false" { $$ = CT(FALSE, NULL); }

NumberLiteral <- (n:FloatingPoint / n:Integral) {
    $$ = n;
}

FloatingPoint <- (f:DecimalPointLeading / f:DecimalPointTrailing) {
    $$ = CT(FLOATING_POINT, $0);
}
DecimalPointLeading <- [0-9]* "." [0-9]+ ExponentPart?
DecimalPointTrailing <- [0-9]+ ExponentPart
ExponentPart <- [eE] [+-]? [0-9]+

Integral <- (HexIntegral / BinaryIntegral / OctalIntegral / DecimalIntegral) {
    $$ = CT(INTEGRAL, $0);
}
HexIntegral <- "0x" [0-9a-fA-F]+
BinaryIntegral <- "0b" [01]+
OctalIntegral <- "0" [0-7]+
DecimalIntegral <- [0-9]+

CharacterLiteral <- "'" ( c:EscapeCharacter / c:NonQuoteCharacter ) "'" {
    $$ = CCN(CHARACTER_LITERAL, 1, c);
}
EscapeCharacter <- "\\" ( [abfnrtv'"\\] /
                   "x" HexDigit HexDigit /
                   "u" HexDigit HexDigit HexDigit HexDigit /
                   "U" HexDigit HexDigit HexDigit HexDigit
                       HexDigit HexDigit HexDigit HexDigit /
                    OctalDigit /
                    OctalDigit OctalDigit /
                    OctalDigit OctalDigit OctalDigit ) {
    $$ = CT(ESCAPE_CHARACTER, $0);
}
HexDigit <- [0-9a-fA-F]
OctalDigit <- [0-7]
NonQuoteCharacter <- [^'] {
    $$ = CT(NON_QUOTE_CHARACTER, $0);
}

StringLiteral <- '"' RegularString '"' / 'r"' RawString '"'
RegularString <- (EscapeSequence / [^"])* {
    $$ = CT(REGULAR_STRING, $0);
}
EscapeSequence <- "\\" [btnvfr"\\]
RawString <- [^"]* {
    $$ = CT(RAW_STRING, $0);
}

FunctionLiteral <- "fn" _ "(" _ p:Params? _ ")" _ e:BlockExpr {
    $$ = CCN(FUNCTION_LITERAL, 2, p, e);
}

EnumLiteral <- edn:EnumDefName _ "." _ en:EnumName {
    $$ = CCN(ENUM_LITERAL, 2, edn, en);
}

TupleLiteral <- "<" _ e:Exprs? _ ">" {
    $$ = CCN(TUPLE_LITERAL, 1, e);
}
Exprs <- e:Expr { AN(EXPRS, e); } (_ "," _ e:Expr { AN(EXPRS, e); })* {
    $$ = CSN(EXPRS);
}

ListLiteral <- "[" _ e:Exprs? _ "]" {
    $$ = CCN(LIST_LITERAL, 1, e);
}

MapLiteral <- "[:]" / "[" _ k:MapKeyValues? _ "]" {
    $$ = CCN(MAP_LITERAL, 1, k);
}
MapKeyValues <- m:MapKeyValue { AN(MAP_KEY_VALUES, m); } (_ "," _ m:MapKeyValue { AN(MAP_KEY_VALUES, m); })* {
    $$ = CSN(MAP_KEY_VALUES);
}
MapKeyValue <- (k:Literal / k:Identifier) _ ":" _ v:Expr {
    $$ = CCN(MAP_KEY_VALUE, 2, k, v);
}

#
# Match expression
#

MatchExpr <- MatchLiteral / UnboundedName / BoundName
MatchExprs <- MatchExpr (_ "," _ MatchExpr)*

MatchLiteral <- MatchBaseLiteral / MatchCompositeLiteral

MatchBaseLiteral <- BooleanLiteral /
                    NumberLiteral /
                    CharacterLiteral /
                    StringLiteral /
                    EnumLiteral

MatchCompositeLiteral <- MatchTupleLiteral /
                         (BoundName _)? MatchListLiteral /
                         (BoundName _)? MatchMapLiteral

MatchListLiteral <- "[" _ MatchExprs? _ "]"

MatchTupleLiteral <- "<" _ MatchExprs? _ ">"

MatchMapLiteral <- "[:]" / "[" _ MatchKeyValues? _ "]"
MatchKeyValues <- MatchKeyValue (_ "," _ MatchKeyValue)*
MatchKeyValue <- (MatchLiteral / Identifier) _ ":" _ MatchExpr

#
# Class definition
#

ClassDef <- "class" __ Identifier _ ( ":" _ Interfaces _)?
                   "{" _ ClassMembers _ "}"
Interfaces <- Identifier (_ "," _ Identifier)*
ClassMembers <- ClassMember (_ ClassMember / ClassMemberError)*
ClassMember <- Constructor / Deconstructor / MemberMethod / MemberProperty
Constructor <- "this" _ "(" _ Params? _ ")" _ BlockExpr
Deconstructor <- "~this" _ "(" _ Params? _ ")" _ BlockExpr
MemberMethod <- MemberAccess _ FunctionDef
MemberAccess <- "public" / "private"
MemberProperty <- (MemberAccess (_ "const")? / "readonly") _ Identifier
                  (_ "=" _ Expr)?
ClassMemberError <- ("," / ";") {
    panic("Unexpected %s between members on line %d", $0, auxil->row);
}

#
# Interface definition
#

InterfaceDef <- "interface" __ Identifier _ "{" _ InterfaceMembers _ "}"
InterfaceMembers <- InterfaceMember (_ InterfaceMember / InterfaceMemberError)*
InterfaceMember <- InterfaceMemberMethod / InterfaceMemberProperty
InterfaceMemberMethod <- MemberAccess _ InterfaceMethod
InterfaceMethod <- "fn" _ Identifier _ "(" _ Params? _ ")"
InterfaceMemberProperty <- (MemberAccess (_ "const")? / "readonly") _ Identifier
InterfaceMemberError <- ("," / ";") {
    panic("Unexpected %s between members on line %d", $0, auxil->row);
}

#
# Enumeration definition
#

EnumDef <- "enum" __ ed:EnumDefName _ "{" _ e:Enums _ "}" {
    $$ = CCN(ENUM_DEF, 2, ed, e);
}
EnumDefName <- Identifier {
    $$ = CT(ENUM_DEF_NAME, $0);
}
Enums <- e:Enum { AN(ENUMS, e); } (__ e:Enum { AN(ENUMS, e); } / EnumValueError)* {
    $$ = CSN(ENUMS);
}
Enum <- en:EnumName (_ "=" _ e:Expr)? {
    $$ = CCN(ENUM, 2, en, e);
}
EnumName <- Identifier {
    $$ = CT(ENUM_NAME, $0);
}
EnumValueError <- ("," / ";") {
    panic("Unexpected %s between enum values on line %d", $0, auxil->row);
}

#
# Function definition
#

FunctionDef <- ("export" _)?
               "fn" __ f:FunctionName _ "(" _ p:Params? _ ")" _ b:BlockExpr {
    $$ = CCN(FUNCTION_DEF, 3, f, p, b);
}
FunctionName <- Identifier {
    $$ = CT(FUNCTION_NAME, $0);
}
Params <- n:NonDefaultParams _ "," _ d:DefaultParams { $$ = CCN(PARAMS, 2, n, d); } /
          n:NonDefaultParams { $$ = n; } /
          d:DefaultParams { $$ = d; }
NonDefaultParams <- n:NonDefaultParam { AN(NON_DEFAULT_PARAMS, n); }
                    (_ "," _ n:NonDefaultParam { AN(NON_DEFAULT_PARAMS, n); })* {
    $$ = CSN(NON_DEFAULT_PARAMS);
}
NonDefaultParam <- Identifier !(_ "=") {
    $$ = CT(NON_DEFAULT_PARAM, $0);
}
DefaultParams <- d:DefaultParam { AN(DEFAULT_PARAMS, d); } (_ "," _ d:DefaultParam {AN(DEFAULT_PARAMS, d); })* {
    $$ = CSN(DEFAULT_PARAMS);
}
DefaultParam <- i:DefaultParamName _ "=" _ e:Expr {
    $$ = CCN(DEFAULT_PARAM, 2, i, e);
}
DefaultParamName <- Identifier {
    $$ = CT(DEFAULT_PARAM_NAME, $0);
}

BlockExpr <- "{" _ BlockLevelExprs _ "}"
BlockLevelExprs <- BlockLevelExpr (_ Comma _ BlockLevelExpr)*
BlockLevelExpr <- FunctionDef / Expr
Comma <- "," / ";" {
    panic("Unexpected ';' on line %d (use ',' as a separator between expressions)",
          auxil->row);
}

Args <- PositionalArgs / NamedArgs
PositionalArgs <- !NamedArg Expr (_ "," _ Expr)*
NamedArgs <- NamedArg (_ "," _ NamedArg)*
NamedArg <- Identifier _ ":" _ Expr

#
# Misc
#

Identifier <- [a-zA-Z_][a-zA-Z_0-9_]*
#_ <- WS*
#__ <- WS+
_ <- (WS / Comments)*
__ <- (WS / Comments)+
WS <- [ \t\r\n] {
    if (strcmp($0, "\n") == 0) {
        auxil->row++;
    }
}
Comments <- SingleLineComment / BlockComment
SingleLineComment <- "//" (!EOL .)* EOL?
EOL <- "\r\n" / "\n" / "\r" {
    auxil->row++;
}
BlockComment <- "/*" (BlockCommentContent / EOL)* "*/"
BlockCommentContent <- (!("*/" / EOL) .)
EOF <- _ !.

%%
int main() {
    satie_auxil_t* satie_auxil = satie_auxil_new();
    satie_context_t *context = satie_create(satie_auxil);
    ast_node_t* program;
    satie_parse(context, &program);
    print_ast(program, 0);
    satie_destroy(context);
    return 0;
}
